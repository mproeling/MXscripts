!MX SCRIPT VOOR HET GESATUREERDE MODEL VOOR DE TALENTENDATA.
!TALENT: MUZIEK, Voorbereiding parent-offspring model
!MP Roeling, Maart 2011, VU-NTR

! --------------------------------------------------------------------------------------------------
! ESTIMATED PARAMETERS: 	13	
! correlation parent-twin
! correlation spouses
! correlation MZ 
! correlation DZ
! means model: Threshold 1 Male and Female twins
! means model: Threshold 2 Male and Female twins
! means model: Threshold 1 Vader
! means model: Threshold 2 Vader
! means model: Threshold 1 Moeder
! means model: Threshold 2 Moeder
! means model: Age B Male and Female twins
! means model: Age B Vader
! means model: Age B moeder
!---------------------------------------------------------------------------------------------------
! TOETSEN
! 1. Threshold vader = moeder = twins
! 2. drop age Vader
! 3. drop age Moeder
! 4. drop age Twins
! 5. MZ = DZ

#define nvarx 1			! one phenotype per subject
#define nthres 2			! number of thresholds
#define nind 4			! numer of individuals

G1: MZM
NGroups = 6				! nr of groups
Data NInput_vars= 19	! nr of inputvars
Missing=-1
ORdinal_data file=music.dat
LABELS reg zyg zygmzdz zage.31 zage.41 zage.1 zage.2 age.31 age.41 age.1 age.2 sex.31 sex.41 sex.1 sex.2 father.31 mother.41 twin.1 twin.2 

Select if zyg = 1; 		! select MZM
Select 	father.31 mother.41 twin.1 twin.2
 		zage.31 zage.41 zage.1 zage.2  ;	
  Definition zage.31 zage.41 zage.1 zage.2 ;

 Begin Matrices ;
    A Stand 2 2 Free ;					!Parent cor
    P Full  2 2 Free; 					!Parent-twin correlation
    C Stand 2 2 Free; 					!Twin cor
    T Full nthres nind Free ;			!Thresholds (4 family members)
    H Full nthres nind Free ; 			!age effect beta
    G Full nthres nind Fix ; 				!observed age effect per family
    J Lower nthres nthres Fixed ;		!2x2martrix met de thresholds
 End Matrices ;

Value 1 J 1 1 - J nthres nthres			!2x2 matrix
specify T 	120 130 140 140  		
             		121 131 141 141			!Een aparte threshold voor de ouders die over alle groepen hetzelfde is, en aparte threshold voor jongens en meiden afhankelijk van de groep.
specify P	80 80				
			80 80					
specify H	160 161 162 162 		
			160 161 162 162		
specify G 	zage.31 zage.41 zage.1 zage.2
			zage.31 zage.41 zage.1 zage.2			

 Begin Algebra;
 R= A|P'_ P|C';
 End Algebra;

 Covariances R ;
 Thresholds J*T+H.G;		

 Start 0.5 A 1 2
 Start 0.2 P 1 1 P 1 2 P 2 2 P 2 1
 Start 0.1 C 1 2
 Start -0.5 T 1 1 T 1 2 T 1 3 T 1 4 
 Start 0.1 T 2 1 T 2 2 T 2 3 T 2 4 
 Start 0.1 H 1 1 - H 2 4 
  Bound -0.999 0.999 C 1 2 P 1 2 A 1 2
  Bound -5.555 5.555 T 1 1 T 1 2 T 1 3 T 1 4 
  Bound 0.1 5.555 T 2 1 T 2 2 T 2 3 T 2 4 

OPT func=1.E-8
End
!--------------------------------------------------------------------------------------------------------------------
G2: DZM
Data NInput_vars= 19
Missing=-1
ORdinal_data file=music.dat
LABELS reg zyg zygmzdz zage.31 zage.41 zage.1 zage.2 age.31 age.41 age.1 age.2 sex.31 sex.41 sex.1 sex.2 father.31 mother.41 twin.1 twin.2 

select if zyg = 2; 
Select 	father.31 mother.41 twin.1 twin.2
 		zage.31 zage.41 zage.1 zage.2  ;	
  Definition zage.31 zage.41 zage.1 zage.2 ;

 Begin Matrices ;
    A Stand 2 2 = A1 ;					!Parent cor = group 1
    P Full  2 2 = P1 ; 					!Parent-twin correlation = group 1
    C Stand 2 2 Free ; 					!Twin cor
    T Full nthres nind = T1 ;			!Thresholds (4 family members) = group 1
    H Full nthres nind = H1 ; 			!age effect beta
    G Full nthres nind Fix ; 				!observed age effect per family
    J Lower nthres nthres Fixed ;
 End Matrices ;

Value 1 J 1 1 - J nthres nthres
specify G 	zage.31 zage.41 zage.1 zage.2
			zage.31 zage.41 zage.1 zage.2		 	

Start 0.3 C 1 2

Begin Algebra;
 R= A|P'_ P|C';
 End Algebra;

 Covariances R ;
 Thresholds J*T+H.G;		

End
!--------------------------------------------------------------------------------------------------------------------
G3: MZF
Data NInput_vars= 19
Missing=-1
ORdinal_data file=music.dat
LABELS reg zyg zygmzdz zage.31 zage.41 zage.1 zage.2 age.31 age.41 age.1 age.2 sex.31 sex.41 sex.1 sex.2 father.31 mother.41 twin.1 twin.2 

Select if zyg = 3; 
Select 	father.31 mother.41 twin.1 twin.2
 		zage.31 zage.41 zage.1 zage.2  ;	
  Definition zage.31 zage.41 zage.1 zage.2 ;

 Begin Matrices ;
    A Stand 2 2 = A1 ;				!Parent cor = group 1
    P Full  2 2 = P1 ; 				!Parent-twin correlation = group 1
    C Stand 2 2 = C1 ; 				!Twin cor
    T Full nthres 4 = T1 ;			!Thresholds (4 family members) = group 1
    H Full nthres nind = H1 ; 		!age effect beta
    G Full nthres nind Fix ; 			!observed age effect per family
    J Lower nthres nthres Fixed ;
 End Matrices ;

Value 1 J 1 1 - J nthres nthres
specify G 	zage.31 zage.41 zage.1 zage.2
			zage.31 zage.41 zage.1 zage.2		

Begin Algebra;
 R= A|P'_ P|C';
 End Algebra;

 Covariances R ;
 Thresholds J*T+H.G;		

end

!--------------------------------------------------------------------------------------------------------------------
G4: DZF
Data NInput_vars= 19
Missing=-1
ORdinal_data file=music.dat
LABELS reg zyg zygmzdz zage.31 zage.41 zage.1 zage.2 age.31 age.41 age.1 age.2 sex.31 sex.41 sex.1 sex.2 father.31 mother.41 twin.1 twin.2 

Select if zyg = 4 ; 
Select 	father.31 mother.41 twin.1 twin.2
 		zage.31 zage.41 zage.1 zage.2  ;	
  Definition zage.31 zage.41 zage.1 zage.2 ;

 Begin Matrices ;
    A Stand 2 2 = A1 ;				!Parent cor  = group 1
    P Full  2 2 = P1 ; 				!Parent-twin correlation = group 1
    C Stand 2 2 = C2 ; 				!Twin cor
    T Full nthres 4 = T1 ;			!Thresholds (4 family members) = group 1
    H Full nthres nind = H1 ; 		!age effect beta
    G Full nthres nind Fix ; 			!observed age effect per family
    J Lower nthres nthres Fixed ;
 End Matrices ;

Value 1 J 1 1 - J nthres nthres
specify G 	zage.31 zage.41 zage.1 zage.2
			zage.31 zage.41 zage.1 zage.2		

Begin Algebra;
 R= A|P'_ P|C';
 End Algebra;

 Covariances R ;
 Thresholds J*T+H.G;		

End

!--------------------------------------------------------------------------------------------------------------------
G5: DOS-MF
Data NInput_vars= 19
Missing=-1
ORdinal_data file=music.dat
LABELS reg zyg zygmzdz zage.31 zage.41 zage.1 zage.2 age.31 age.41 age.1 age.2 sex.31 sex.41 sex.1 sex.2 father.31 mother.41 twin.1 twin.2 

Select if zyg = 5; 
Select 	father.31 mother.41 twin.1 twin.2
 		zage.31 zage.41 zage.1 zage.2  ;	
  Definition zage.31 zage.41 zage.1 zage.2 ;

 Begin Matrices ;
    A Stand 2 2 = A1;				!Parent cor  = group 1
    P Full  2 2 = P1 ; 				!Parent-twin correlation = group 1
    C Stand 2 2 = C2 ;  				!Twin cor
    T Full nthres 4 = T1 ;			!Thresholds (4 family members) = group 1
    H Full nthres nind = H1 ; 		!age effect beta
    G Full nthres nind Fix ; 			!observed age effect per family
    J Lower nthres nthres Fixed ;
 End Matrices ;

Value 1 J 1 1 - J nthres nthres
specify G 	zage.31 zage.41 zage.1 zage.2
			zage.31 zage.41 zage.1 zage.2		

Begin Algebra;
 R= A|P'_ P|C';
 End Algebra;

 Covariances R ;
 Thresholds J*T+H.G;		

End

!--------------------------------------------------------------------------------------------------------------------
G6: DOS-FM
Data NInput_vars= 19
Missing=-1
ORdinal_data file=music.dat
LABELS reg zyg zygmzdz zage.31 zage.41 zage.1 zage.2 age.31 age.41 age.1 age.2 sex.31 sex.41 sex.1 sex.2 father.31 mother.41 twin.1 twin.2 

select if zyg = 6; 
Select 	father.31 mother.41 twin.1 twin.2
 		zage.31 zage.41 zage.1 zage.2  ;	
  Definition zage.31 zage.41 zage.1 zage.2 ;

 Begin Matrices ;
    A Stand 2 2 = A1 ;				!Parent cor  = group 1
    P Full  2 2 = P1 ; 				!Parent-twin correlation = group 1
    C Stand 2 2 = C2 ; 				!Twin cor
    T Full nthres 4 = T1 ;			!Thresholds (4 family members) = group 1
    H Full nthres nind = H1 ; 		!age effect beta
    G Full nthres nind Fix ; 			!observed age effect per family
    J Lower nthres nthres Fixed ;
 End Matrices ;

Value 1 J 1 1 - J nthres nthres
specify G 	zage.31 zage.41 zage.1 zage.2
			zage.31 zage.41 zage.1 zage.2		

Begin Algebra;
 R= A|P'_ P|C';
 End Algebra;

Covariances R ;
Thresholds J*T+H.G;		

Option Multiple
Option Issat

End

! -------------------------------------------------------------------------------------------------------------------------------

! Parameter intervals @95
! Interval @95 C 1 2 1 		! correlatie MZs
! Interval @95 C 2 2 1		! correlatie DZs
! Interval @95 A 1 2 1 		! correlatie spouses
! Interval @95 P 1 1 1 		! vier correlaties van de ouder naar de het kind zijn gelijkgesteld op parameter specificatie "80"
! Interval @95 T 1 1 1			! t1 vader  
! Interval @95 T 1 1 2 		! t1 moeder
! Interval @95 T 1 1 3			! t1 zoons en dochters
! Interval @95 T 1 2 1 		! t2 vader
! Interval @95 T 1 2 2 		! t2 moeder
! Interval @95 T 1 2 3 		! t2 zoons en dochters
! Interval @95 H 1 1 1 		! age B vader 
! Interval @95 H 1 1 2		! age B moeder
! Interval @95 H 1 1 3		! age B zoon en dochters 
! end

save musicfull.mxs

! 1. Threshold vader = moeder = twins
get musicfull.mxs
EQ T 1 1 1 T 1 1 2 T 1 1 3 T 1 1 4 T 2 1 1 T 2 1 2 T 2 1 3 T 2 1 4 T 3 1 1 T 3 1 2 T 3 1 3 T 3 1 4 T 4 1 1 T 4 1 2 T 4 1 3 T 4 1 4 T 5 1 1 T 5 1 2 T 5 1 3 T 5 1 4 T 6 1 1 T 6 1 2 T 6 1 3 T 6 1 4 
EQ T 1 2 1 T 1 2 2 T 1 2 3 T 1 2 4 T 2 2 1 T 2 2 2 T 2 2 3 T 2 2 4 T 3 2 1 T 3 2 2 T 3 2 3 T 3 2 4 T 4 2 1 T 4 2 2 T 4 2 3 T 4 2 4 T 5 2 1 T 5 2 2 T 5 2 3 T 5 2 4 T 6 2 1 T 6 2 2 T 6 2 3 T 6 2 4
end
! 2. drop age Vader
get musicfull.mxs
drop @0 H 1 1 1 
end
save musicfull2.mxs
! 3. drop age Moeder
get musicfull2.mxs
drop @0 H 1 1 2
end
save musicfull3.mxs
! 4. drop age Twins
get musicfull3.mxs
drop @0 H 1 1 3 
end
! 5. MZ = DZ
get musicfull.mxs
EQ C 1 2 1 C 2 2 1 
end
